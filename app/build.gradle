plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def apiKeyProperties = new Properties()
def apiKeyPropertiesFile = rootProject.file('api_key.properties')
apiKeyPropertiesFile.withReader('UTF-8') { reader ->
    apiKeyProperties.load(reader)
}
def apiKey = apiKeyProperties.getProperty('apiKey')

apply from: "$project.rootDir/jacoco.gradle"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 32

    defaultConfig {
        applicationId "hu.bme.aut.recipebase"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'BASE_URL', "\"https://rapidapi.com/apidojo/api/tasty/\""
        buildConfigField "String", "API_KEY", "\"$apiKey\""
        buildConfigField "String", "AUTH_NAME", "\"api_key\""
        buildConfigField "String", "DB_NAME", "\"recipes.db\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    def retrofit_version = '2.9.0'
    def room_version = '2.4.2'
    def archVersion = '2.2.0'
    def kotest_version = '4.2.5'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "com.google.dagger:hilt-android:2.41"
    kapt "com.google.dagger:hilt-compiler:2.38.1"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation "io.reactivex:rxjava:1.3.8"
    implementation "io.swagger:swagger-annotations:1.5.24"
    implementation ("org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1"){
        exclude group:'org.apache.oltu.oauth2' , module: 'org.apache.oltu.oauth2.common'
    }
    implementation "io.gsonfire:gson-fire:1.8.5"
    implementation "org.threeten:threetenbp:1.4.1"

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Arch components
    implementation "androidx.lifecycle:lifecycle-extensions:$archVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archVersion"
    implementation "androidx.activity:activity-ktx:1.4.0"

    // Kotest - Unit Test Framework
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
    testImplementation "io.kotest:kotest-property:$kotest_version"

    // MockK - Mocks
    testImplementation "io.mockk:mockk:1.9.3"

    // Google Truth - Asserts
    testImplementation "com.google.truth:truth:1.1.3"

    // JUnit - UI Test Framework
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.platform:junit-platform-commons:1.6.2'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    // Jetpack Compose
    implementation("androidx.compose.ui:ui:$compose_version")
    implementation("androidx.compose.ui:ui-tooling:$compose_version")
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation("androidx.compose.foundation:foundation:$compose_version")
    implementation("androidx.compose.material:material:$compose_version")
    implementation("androidx.compose.material:material-icons-core:$compose_version")
    implementation("androidx.compose.material:material-icons-extended:$compose_version")
    implementation("androidx.compose.runtime:runtime-livedata:$compose_version")
    implementation("androidx.compose.runtime:runtime-rxjava2:$compose_version")
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.navigation:navigation-compose:2.5.0-rc01"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    implementation("io.coil-kt:coil-compose:2.0.0-rc01")
    implementation "org.jetbrains.kotlin:kotlin-script-runtime:1.6.21"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:30.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
}